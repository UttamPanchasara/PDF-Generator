plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'com.uttampanchasara.pdfgenerator'
    compileSdk 36

    defaultConfig {
        minSdk 26

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // compileOnly = consumer must provide these dependencies (avoids version conflicts)
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.2"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.10.2"
    compileOnly "androidx.annotation:annotation:1.9.1"
}

// JitPack publishing configuration
afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.github.UttamPanchasara'
                artifactId = 'pdfgenerator'
                version = '2.0.0'

                pom {
                    name = 'PDF Generator'
                    description = 'A simple Android library to generate PDF from HTML content'
                    url = 'https://github.com/UttamPanchasara/PDF-Generator'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'uttampanchasara'
                            name = 'Uttam Panchasara'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/UttamPanchasara/PDF-Generator.git'
                        developerConnection = 'scm:git:ssh://github.com/UttamPanchasara/PDF-Generator.git'
                        url = 'https://github.com/UttamPanchasara/PDF-Generator/tree/master'
                    }
                }
            }
        }
    }
}
